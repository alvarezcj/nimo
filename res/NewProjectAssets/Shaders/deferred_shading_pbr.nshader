@VERTEX
//https://github.com/JoeyDeVries/LearnOpenGL/tree/master/src/6.pbr
#version 330 core
layout (location = 0) in vec3 aPos;
layout (location = 1) in vec3 aNormal;
layout (location = 2) in vec2 aTexCoords;
layout (location = 3) in vec3 aTangent;
layout (location = 4) in vec3 aBitangent;

out vec2 TexCoords;

void main()
{
    TexCoords = aTexCoords;
    gl_Position = vec4(aPos, 1.0);
}
@VERTEXEND@FRAGMENT
#version 330 core
out vec4 FragColor;

in vec2 TexCoords;

// G-Buffer samplers
uniform sampler2D gPosition;
uniform sampler2D gNormal;
uniform sampler2D gAlbedo;
uniform sampler2D gARM;
uniform sampler2D gDepth;
// Directional Light
uniform sampler2D directionalLightShadowMap;
uniform samplerCube irradianceMap;
uniform mat4 directionalLightSpaceMatrix;
uniform vec3 directionalLightPos;
uniform vec3 directionalLightColor;
uniform float directionalLightIntensity;
uniform mat4 InvProjection;

struct Light {
    vec3 Position;
    vec3 Color;
	float Intensity;
    
    float Linear;
    float Quadratic;
    float Radius;

    bool Active;
};
const float PI = 3.14159265359;
const int NR_LIGHTS = 32;
uniform Light lights[NR_LIGHTS];
uniform vec3 viewPos;
uniform int currentLights;

float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness*roughness;
    float a2 = a*a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}
float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}
float ShadowMapVisibility(vec3 P, vec3 N) 
{
  vec4 PLw = (directionalLightSpaceMatrix * vec4(P, 1.0));
  vec3 PL = (PLw.xyz / PLw.w) * 0.5 + 0.5;
  if (PL.z > 1.0 || PL.z < 0.0) return 0.0;
  float closestDepth = texture(directionalLightShadowMap, PL.xy).r;
  vec3 L = normalize(directionalLightPos.xyz - P);
  float bias = max(0.005 * (1.0 - dot(N, L)), 0.003);
  return ((PL.z - bias ) > closestDepth) ? 1.0 : 0.0;
}
float ShadowCalculation(vec4 fragPosLightSpace, vec3 fragPosWorldSpace, vec3 fragNormal)
{
    // perform perspective divide
    vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
    // transform to [0,1] range
    projCoords = projCoords * 0.5 + 0.5;
    // get closest depth value from light's perspective (using [0,1] range fragPosLight as coords)
    //float closestDepth = texture(shadowMap, projCoords.xy).r; 
    // get depth of current fragment from light's perspective
    float currentDepth = projCoords.z;
    // calculate bias (based on depth map resolution and slope)
    vec3 normal = normalize(fragNormal);
    vec3 lightDir = normalize(directionalLightPos - fragPosWorldSpace);
    float bias = max(0.0005 * (1.0 - dot(normal, lightDir)), 0.00005);
    // PCF    
    float Pi = 6.28318530718; // Pi*2
    float Directions = 16.0;
    float Quality = 4.0;
    float Size = 4.0;
    vec2 Radius = Size/textureSize(directionalLightShadowMap, 0);
    float shadow = texture(directionalLightShadowMap, projCoords.xy).r;
    // Blur calculations
    for( float d=0.0; d<Pi; d+=Pi/Directions)
    {
		for(float i=1.0/Quality; i<=1.001; i+=1.0/Quality)
        {
            float pcfDepth = texture(directionalLightShadowMap, projCoords.xy +vec2(cos(d),sin(d))*Radius*i).r; 
            shadow += currentDepth - bias > pcfDepth  ? 1.0 : 0.0;       	
        }
    }
    shadow /= Quality * Directions +1.0;
    
    // keep the shadow at 0.0 when outside the far_plane region of the light's frustum.
    if(projCoords.z > 1.0)
        shadow = 0.0;
        
    return shadow;
}
// Reconstruct view space position from depth
vec3 ViewPositionFromDepth(vec2 texcoord, float depth)
{
  // Get x/w and y/w from the viewport position
  vec3 projectedPos = vec3(texcoord, depth) * 2.0f - 1.0f;;

  // Transform by the inverse projection matrix
  vec4 positionVS = InvProjection * vec4(projectedPos, 1.0f);

  // Divide by w to get the view-space position
  return positionVS.xyz / positionVS.w;
}

void main()
{             
    vec4 albedoSample = texture(gAlbedo, TexCoords);
    if(albedoSample.a == 0.0) 
        discard;
    vec3 WorldPos = texture(gPosition, TexCoords).rgb;
    vec4 worldPosLightSpace = directionalLightSpaceMatrix * vec4(WorldPos, 1.0);
    vec3 albedo     = pow(albedoSample.rgb, vec3(2.2));
    float metallic  = texture(gARM, TexCoords).b;
    float roughness = texture(gARM, TexCoords).g;
    float ao        = texture(gARM, TexCoords).r;
    vec3 N = normalize(texture(gNormal, TexCoords).xyz);
    float D = texture(gDepth, TexCoords).x;

    // Recompute viewspace position from UV + depth
    vec3 P = ViewPositionFromDepth(TexCoords, D);
    float shadow = ShadowCalculation(worldPosLightSpace, WorldPos, N);
    //vec3 V = normalize(viewPos - texture(gPosition, TexCoords).rgb);
    vec3 V = normalize(-P);
    // Lerp between Dia-electric = 0.04f to Metal = albedo
    vec3 F0 = mix(vec3(0.04), albedo, metallic);
    // reflectance equation
    vec3 Lo = vec3(0.0);
	// Directional light
    vec3 L = normalize(directionalLightPos - WorldPos);
    vec3 H = normalize(V + L);
    vec3 radiance = (1.0 - shadow) * directionalLightColor * directionalLightIntensity;

    // Cook-Torrance BRDF
    float NDF = DistributionGGX(N, H, roughness);   
    float G   = GeometrySmith(N, V, L, roughness);      
    vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);
    
    vec3 numerator    = NDF * G * F; 
    float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.0001; // + 0.0001 to prevent divide by zero
    vec3 specular = numerator / denominator;

    // kS is equal to Fresnel
    vec3 kS = F;
    // for energy conservation, the diffuse and specular light can't
    // be above 1.0 (unless the surface emits light); to preserve this
    // relationship the diffuse component (kD) should equal 1.0 - kS.
    vec3 kD = vec3(1.0) - kS;
    // multiply kD by the inverse metalness such that only non-metals 
    // have diffuse lighting, or a linear blend if partly metal (pure metals
    // have no diffuse light).
    kD *= 1.0 - metallic;	  

    // scale light by NdotL
    float NdotL = max(dot(N, L), 0.0);        

    // add to outgoing radiance Lo
    Lo += (kD * albedo / PI + specular) * radiance * NdotL;  // note that we already multiplied the BRDF by the Fresnel (kS) so we won't multiply by kS again
	//Point lights
    for(int i = 0; i < NR_LIGHTS; ++i)
    {
        if(!lights[i].Active) continue;
        // calculate distance between light source and current fragment
        float distance = length(lights[i].Position - WorldPos);
        if(distance < lights[i].Radius)
        {
        	// calculate per-light radiance
        	vec3 L = normalize(lights[i].Position - WorldPos);
        	vec3 H = normalize(V + L);
			float attenuation = lights[i].Intensity / (1.0 + lights[i].Linear * distance + lights[i].Quadratic * distance * distance);
        	//float attenuation = 1.0 / (distance * distance);
        	vec3 radiance = lights[i].Color * attenuation;

        	// Cook-Torrance BRDF
        	float NDF = DistributionGGX(N, H, roughness);   
        	float G   = GeometrySmith(N, V, L, roughness);      
        	vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);
           
        	vec3 numerator    = NDF * G * F; 
        	float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.0001; // + 0.0001 to prevent divide by zero
        	vec3 specular = numerator / denominator;
        
        	// kS is equal to Fresnel
        	vec3 kS = F;
        	// for energy conservation, the diffuse and specular light can't
        	// be above 1.0 (unless the surface emits light); to preserve this
        	// relationship the diffuse component (kD) should equal 1.0 - kS.
        	vec3 kD = vec3(1.0) - kS;
        	// multiply kD by the inverse metalness such that only non-metals 
        	// have diffuse lighting, or a linear blend if partly metal (pure metals
        	// have no diffuse light).
        	kD *= 1.0 - metallic;	  

        	// scale light by NdotL
        	float NdotL = max(dot(N, L), 0.0);        

        	// add to outgoing radiance Lo
        	Lo += (kD * albedo / PI + specular) * radiance * NdotL;  // note that we already multiplied the BRDF by the Fresnel (kS) so we won't multiply by kS again
        }
    }
    vec3 irradiance = texture(irradianceMap, N).rgb;
    vec3 diffuse      = irradiance * albedo;
    vec3 ambient = (diffuse) * ao * (1.0f - shadow);
    
    vec3 color = ambient + Lo;
    FragColor = vec4(vec3(color),1.0);
}
@FRAGMENTEND